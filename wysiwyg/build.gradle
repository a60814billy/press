apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'org.jetbrains.kotlin.native.cocoapods'
apply plugin: 'kotlin-android-extensions'

android {
  compileSdkVersion projectVersions.compileSdk
  defaultConfig {
    minSdkVersion projectVersions.minSdk
  }

  lintOptions {
    lintConfig file("lint.xml")
  }
}

// Not sure, but probably used by CocoaPods.
version = 0.1

kotlin {
  android("android")
  // This is for iPhone emulator
  // Switch here to iosArm64 (or iosArm32) to build library for iPhone device
  iosX64("ios")

  sourceSets {
    commonMain {
      dependencies {
        implementation kotlin('stdlib-common')
      }
    }
    commonTest {
      dependencies {
        implementation kotlin('test-common')
        implementation kotlin('test-annotations-common')
      }
    }
    androidMain {
      dependencies {
        implementation kotlin('stdlib')
        implementation deps.androidx.appCompat
        implementation deps.flexmark.core
        implementation deps.flexmark.strikethrough
        implementation deps.flexmark.autoLink
        implementation deps.timber
        implementation deps.assertK.common
        implementation deps.assertK.opentest4j  // https://youtrack.jetbrains.com/issue/IDEA-214533
      }
    }
    androidTest {
      dependencies {
        implementation kotlin('test')
        implementation kotlin('test-junit')
      }
    }
    iosMain {}
    iosTest {}

    all {
      languageSettings {
        enableLanguageFeature('InlineClasses')
      }
    }
  }

  cocoapods {
    // Configure fields required by CocoaPods.
    summary = "Wysiwyg"
    homepage = "https://github.com/saket/press"
  }
}

dependencies {}
